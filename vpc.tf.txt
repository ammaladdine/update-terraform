#
# VPC Resources
#  * VPC
#  * Subnets
#  * Internet Gateway
#  * Route Table
#

resource "aws_vpc" "sandbox-bastion" {
  cidr_block = "10.0.0.0/16"

  tags = tomap({
    "Name"                                      = "terraform-eks-sandbox-bastion-node",
    "kubernetes.io/cluster/${var.cluster-name}" = "shared",
  })
}

resource "aws_subnet" "sandbox-bastion" {
  count = 2

  availability_zone       = data.aws_availability_zones.available.names[count.index]
  cidr_block              = "10.0.${count.index}.0/24"
  map_public_ip_on_launch = true
  vpc_id                  = aws_vpc.sandbox-bastion.id

  tags = tomap({
    "Name"                                      = "terraform-eks-sandbox-bastion-node",
    "kubernetes.io/cluster/${var.cluster-name}" = "shared",
  })
}

resource "aws_internet_gateway" "sandbox-bastion" {
  vpc_id = aws_vpc.sandbox-bastion.id

  tags = {
    Name = "terraform-eks-sandbox-bastion"
  }
}

resource "aws_route_table" "sandbox-bastion" {
  vpc_id = aws_vpc.sandbox-bastion.id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.sandbox-bastion.id
  }
}

resource "aws_route_table_association" "sandbox-bastion" {
  count = 2

  subnet_id      = aws_subnet.sandbox-bastion.*.id[count.index]
  route_table_id = aws_route_table.sandbox-bastion.id
}
